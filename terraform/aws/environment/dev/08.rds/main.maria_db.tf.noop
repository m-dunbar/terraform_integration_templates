# =============================================================================
# terraform_integration_templates :: terraform/aws/environment/dev/08.rds/main.maria_db.tf
#       :: mdunbar :: 2025 Oct 26 :: MIT License Â© 2025 Matthew Dunbar ::
# =============================================================================
module "maria_db" {
  source  = "terraform-aws-modules/rds/aws"
  version = "~> 6.13.0"

  identifier           = "maria-db-demo"
  engine               = "mariadb"
  engine_version       = "11.8"
  family               = "mariadb11.8"

  create_db_option_group = false

  instance_class        = "db.t3.micro"
  storage_type          = "gp3"
  allocated_storage     = 20
  max_allocated_storage = 50

  # storage_encrypted = true
  # kms_key_id        = 

  db_name = "wordpress"

  multi_az              = false     # false is fine for dev, but should be true for prod
  publicly_accessible   = false

  auto_minor_version_upgrade = true
  apply_immediately          = false   # set to true to apply outside of maintenance window
  skip_final_snapshot        = true    # ** default maintenance window: Sun 0100-0100 UTC (-4) **

  username                    = "admin"
  manage_master_user_password = true
  master_user_password_rotation_automatically_after_days = 7

  db_subnet_group_name = aws_db_subnet_group.maria_db_subnet_group.name

  vpc_security_group_ids = [aws_security_group.mariadb_sg.id]

  tags = {
    Environment = "dev"
    Managed_by   = "Terraform"
  }
}

data "aws_security_group" "default" {
  vpc_id = data.terraform_remote_state.vpc.outputs.vpc_id

  filter {
    name   = "group-name"
    values = ["default"]
  }
}

resource "aws_security_group" "mariadb_sg" {
  name        = "mariadb-sg"
  description = "Security group for MariaDB RDS"
  vpc_id      = data.terraform_remote_state.vpc.outputs.vpc_id

  ingress {
    description      = "Allow access to MariaDB via SSH"
    from_port        = 22
    to_port          = 22
    protocol         = "tcp"
    security_groups  = [data.aws_security_group.default.id] # your app SG
  }

  ingress {
    description      = "Allow access to MariaDB from app servers"
    from_port        = 3306
    to_port          = 3306
    protocol         = "tcp"
    security_groups  = [data.aws_security_group.default.id] # your app SG
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = {
    Name = "mariadb-sg"
  }
}

resource "aws_db_subnet_group" "maria_db_subnet_group" {
  name       = "maria-db-subnet-group"
  subnet_ids = data.terraform_remote_state.vpc.outputs.private_subnets
  tags = {
    Name = "RDS Subnet Group"
  }
}

output "maria_db_secret_arn" {
  value = module.maria_db.db_instance_master_user_secret_arn
}

output "maria_db_instance_endpoint" {
  value = module.maria_db.db_instance_endpoint
}

# =============================================================================
