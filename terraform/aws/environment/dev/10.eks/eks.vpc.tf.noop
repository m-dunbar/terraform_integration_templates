# =============================================================================
# terraform_integration_templates :: terraform/aws/environment/dev/10.eks/eks.vpc.tf
#       :: mdunbar :: 2026 Feb 23 :: MIT License Â© 2025 Matthew Dunbar ::
# =============================================================================
module "vpc_dev_eks" {
  source  = "terraform-aws-modules/vpc/aws"
  version = "~> 6.5.0"

  name                 = "${local.environment}-${local.region}-eks"
  cidr                 = data.aws_vpc_ipam_preview_next_cidr.dev_private_cidr.cidr
  azs                  = local.azs
  private_subnets      = local.private_subnets
  public_subnets       = local.public_subnets

  private_subnet_names = [
    for az in local.azs : "${local.environment}-${az}-eks-private"
  ]
  public_subnet_names = [
    for az in local.azs : "${local.environment}-${az}-eks-public"
  ]

  default_network_acl_name      = "${local.environment}-${local.region}-eks-nacl"
  default_route_table_name      = "${local.environment}-${local.region}-eks-rtb"
  default_security_group_name   = "${local.environment}-${local.region}-eks-sg"

  enable_nat_gateway   = false    # start without, add if required
  enable_dns_hostnames = true
  enable_dns_support   = true


  private_subnet_tags_per_az = {
    for az in local.azs :
    az => {
      az   = az
      type = "private"
    }
  }

  public_subnet_tags_per_az = {
    for az in local.azs :
    az => {
      az   = az
      type = "public"
    }
  }

  igw_tags = {
    Name = "${local.environment}-${local.region}-eks-igw"
  }

  nat_gateway_tags = {
    Name = "${local.environment}-${local.region}-eks-nat-gw"
  }

  tags = {
    Environment = local.environment
    ManagedBy   = "Terraform"
  }

  vpc_tags = {
    Name = "${local.environment}-${local.region}-eks-vpc"
  }
}

# =============================================================================

